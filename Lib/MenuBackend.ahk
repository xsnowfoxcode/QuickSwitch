; These functions are responsible for the Context Menu functionality and its Options

Dummy() {
    return
}

SwitchPath(ByRef path, _fromMenu := "") {
    global

    local _ex, _winPid, _log := ""
    loop % SelectPathAttempts {
        try {
            if FillDialog(EditId, path, SendEnter)
                return true

        } catch _ex {
            if !WinActive("ahk_id " DialogId)
                return false
        
            if (A_Index = SelectPathAttempts)
                _log := _ex.what " " _ex.message " " _ex.extra
        }
    }

    ; If dialog owner is elevated, show error in Main
    WinGet, _winPid, pid, % "ahk_id " DialogId

    if (IsAppElevated(_winPid)
     || AddElevatedName(_winPid))
        return false

    ; Log additional info and error details (if catched)
    return LogError("Failed to feed the file dialog"
                  , _fromMenu ? "Menu selection" : "AutoSwitch"
                  , "Timeout. " _log)
}

SelectPath(ByRef paths, _fromMenu := "", _pos := 1) {
    global
    
    if (ShowPinned && GetKeyState(PinKey)) {
        if (_pos > PinnedPaths.Length())
            PinnedPaths.InsertAt(1, [paths[_pos][1], "Pin.ico", 1, ""])
        else
            PinnedPaths.RemoveAt(_pos)

        WritePinnedPaths := true

        CreateMenu()
        return ShowMenu()
    }

    if IsDialogClosed
        return SendPath(paths[_pos][1])
    
    SwitchPath(paths[_pos][1], _fromMenu)
    if (ShowAlways || ShowAfterSelect)
        return ShowMenu()
}

;─────────────────────────────────────────────────────────────────────────────
;
SendPath(path) {
;─────────────────────────────────────────────────────────────────────────────
    ; Send path to the current file manager / active window
    WinGet, _id,  % "id", % "A"
    WinGet, _exe, % "ProcessPath", % "A"
    WinGetClass, _class, % "A"
    _quotedExe := """" _exe """"
    path := """" path """"

    switch (_class) {
        case "CabinetWClass":
            SendExplorerPath(_id, path)
        case "ThunderRT6FormDC":
            Run, % _quotedExe " /feed=|::goto " path ";|"
        case "dopus.lister":
            SplitPath, _exe,, _exeDir
            Run, % """" _exeDir "\..\dopusrt.exe"" /acmd go " path
        case "TTOTAL_CMD":
            Run, % _quotedExe " /O /S /L=" path
        default:
            Run, % _quotedExe " " path
    }
}

;─────────────────────────────────────────────────────────────────────────────
;
IsMenuReady() {
;─────────────────────────────────────────────────────────────────────────────
    global
    return ShowAlways && DialogAction != -1
        || ShowNoSwitch && DialogAction = 0
        || ShowAfterSettings && FromSettings
}

;─────────────────────────────────────────────────────────────────────────────
;
ToggleAutoSwitch() {
;─────────────────────────────────────────────────────────────────────────────
    global

    DialogAction := (DialogAction = 1) ? 0 : 1
    WriteDialogAction := true
    
    ; Check only current item
    AddMenuOption("AutoSwitch", "ToggleAutoSwitch", DialogAction = 1)
    AddMenuOption("BlackList",  "ToggleBlackList",  false)
    
    if (DialogAction = 1 && %AutoSwitchTarget%.Length())
        SwitchPath(%AutoSwitchTarget%[AutoSwitchIndex][1])

    if IsMenuReady()
        ShowMenu()
}

;─────────────────────────────────────────────────────────────────────────────
;
ToggleBlackList() {
;─────────────────────────────────────────────────────────────────────────────
    global

    DialogAction := (DialogAction = -1) ? 0 : -1
    WriteDialogAction := true

    ; Check only current item
    AddMenuOption("AutoSwitch", "ToggleAutoSwitch", false)
    AddMenuOption("BlackList",  "ToggleBlackList",  DialogAction = -1)    
    
    if BlackListProcess
        FingerPrint := DialogProcess

    if IsMenuReady()
       ShowMenu()
}
