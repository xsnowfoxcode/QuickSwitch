/*
    Contains getters whose names correspond to classes of known file managers.
    All functions add options to the array. See implementation and documentation in Lib\MenuFrontend
    "winId" param must be existing window uniq ID (window handle / HWND)
*/

GroupAdd, ManagerClasses, ahk_class TTOTAL_CMD
GroupAdd, ManagerClasses, ahk_class CabinetWClass
GroupAdd, ManagerClasses, ahk_class ThunderRT6FormDC
GroupAdd, ManagerClasses, ahk_class dopus.lister

AppendDecodedBytes(ByRef _text, ByRef _array) {
    if !_array.Length()
        return

    VarSetCapacity(_buffer, _array.Length(), 0)
    for _i, _byte in _array
        NumPut(_byte, _buffer, _i - 1, "UChar")

    _text .= StrGet(&_buffer, _array.Length(), "UTF-8")
    _array := []
}

DecodeLocationURL(_url) {
    if !_url
        return ""

    _path := RegExReplace(_url, "i)^file:/+")
    _path := StrReplace(_path, "/", "\")
    _decoded := ""
    _bytes := []
    _index := 1
    _length := StrLen(_path)

    while (_index <= _length) {
        _char := SubStr(_path, _index, 1)
        if (_char = "%") {
            _hex := SubStr(_path, _index + 1, 2)
            if (_hex ~= "i)^[0-9A-F]{2}$") {
                _bytes.Push("0x" _hex)
                _index += 3
                continue
            }
        }

        AppendDecodedBytes(_decoded, _bytes)
        _decoded .= _char
        _index++
    }
    AppendDecodedBytes(_decoded, _bytes)

    return Trim(_decoded, " `t/\")
}

CabinetWClass(ByRef winId, ByRef paths, _activeTabOnly := false, _showLockedTabs := false) {
    ; Analyzes open Explorer windows (tabs) and looks for non-virtual paths
    ; Returns number of added paths
    _activeTab := 0
    _active := 1
    _count  := 0
    _paths  := []

    try
        ControlGet, _activeTab, % "hwnd",, % "ShellTabWindowClass1", % "ahk_id " winId
    catch
        try ControlGet, _activeTab, % "hwnd",, % "TabWindowClass1", % "ahk_id " winId

    try {
        _shellApp := 0
        _shellApp := ComObjCreate("Shell.Application")

        for _win in _shellApp.windows {
            if (winId != _win.hwnd)
                continue
            
            _count++
            if _activeTab {
                ; Get active tab index
                try {
                    static IID_IShellBrowser := "{000214E2-0000-0000-C000-000000000046}"
                    _shell := 0
                    _shell := ComObjQuery(_win, IID_IShellBrowser, IID_IShellBrowser)

                    ; https://www.autohotkey.com/boards/viewtopic.php?style=19&t=109907
                    ; Invoke the method from the vtable of the _shell object, which represents the IShellBrowser interface,
                    ; to retrieve the current tab in Windows Explorer.
                    DllCall(NumGet(NumGet(_shell + 0), 3 * A_PtrSize), "ptr", _shell, "ptr*", _currentTab := 0)

                    if (_currentTab = _activeTab) {
                        _active    := _count
                        _activeTab := 0  ; don't fall into this if-block again
                    } else if _activeTabOnly {
                        continue  ; skip other tabs
                    }
                } finally {
                    if _shell
                        ObjRelease(_shell)
                }
            }

            ; Get current path
            _path := ""
            if _win.locationURL {
                _path := DecodeLocationURL(_win.locationURL)
            }
            if !_path {
                if (_active = _count)
                    _active--
                    
                _count--
                continue
            }

            ; Early return
            if _activeTabOnly {
                paths.push([_path, "Explorer.ico", 1, ""])
                return 1
            }

            _paths.push([_path, "Explorer.ico", 1, ""])
        }
    } finally {
        if _shellApp
            ObjRelease(_shellApp)
    }

    ; Push the active tab to the global array first
    ; Remove duplicate and add the remaining tabs
    if (_paths.hasKey(_active))
        paths.push(_paths.removeAt(_active))

    paths.push(_paths*)
    return _count
}

;─────────────────────────────────────────────────────────────────────────────
;
ThunderRT6FormDC(ByRef winId, ByRef paths, _activeTabOnly := false, _showLockedTabs := false) {
;─────────────────────────────────────────────────────────────────────────────
    ; Sends script to XYplorer and parses the clipboard.
    ; Returns number of added paths.

    ; Save clipboard to restore later
    _clipSaved := ClipboardAll
    A_Clipboard  := ""

    ; $hideLockedTabs is unset by default
    static getAllPaths := "
    ( LTrim Join Comments
        $allPaths = <get tabs | a>, 'r'`;           ; Get tabs from the active panel, resolve native variables
        if (Get('#800')) {                          ; Second pane is enabled
            $allPaths .= '|' . <get tabs | i>`;     ; Get tabs from second pane
        }

        $realPaths = ''`;
        $activePath = ''`;
        $activeIndex = Tab('get')`;
        $index = 0`;

        ForEach($path, $allPaths, '|') {            ; Path separator is |
            $index++`;

            if (!Exists($path)
             || IsSet($hideLockedTabs)
             && (Tab('get', 'flags', $index) % 4 > 0)) {
                continue`;                          ; Exclude this tab
            }
            if ($index == $activeIndex) {
                $activePath = '|' . PathReal($path)`;  ; Save the active tab to insert it as first later
            } else {
                $realPaths .= '|' . PathReal($path)`;  ; Get the real path (XY has special and virtual paths)                
            }
        }

        $realPaths = Trim($activePath . $realPaths, '| ')`;
        if ($realPaths) {
            CopyText $realPaths`;                   ; Place to the clipboard. It's faster then CopyData
        } else {
            CopyText 'unset'`;                      ; No available tabs
        }
    )"

    static getCurPath := "
    ( LTrim Join
        if (!Exists(<curpath>)
         || IsSet($hideLockedTabs)
         && (Tab('get', 'flags') % 4 > 0)) {
            CopyText 'unset'`;
        } else {
            CopyText <curpath>`;
        }
    )"

    _script := _activeTabOnly ? getCurPath : getAllPaths
    _prefix := _showLockedTabs ? "::" : "::$hideLockedTabs = true`;"
    SendXyplorerScript(winId, _prefix . _script)

    ; Try to fetch clipboard data
    ClipWait 1
    _clip       := A_Clipboard
    A_Clipboard := _clipSaved

    ; Retry if empty
    static attempts := 0
    if !(_clip || (attempts = 3)) {
        attempts++
        return ThunderRT6FormDC(winId, paths, _activeTabOnly, _showLockedTabs)
    }

    if (!_clip || (_clip = "unset"))
        return 0

    _count := attempts := 0
    Loop, parse, _clip, `|
    {
        paths.push([A_LoopField, "Xyplorer.ico", 1, ""])
        if _activeTabOnly
            return 1

        _count++
    }

    return _count
}

;─────────────────────────────────────────────────────────────────────────────
;
Dopus(ByRef winId, ByRef paths, _activeTabOnly := false, _showLockedTabs := false) {
;─────────────────────────────────────────────────────────────────────────────
    ; Analyzes the text of address bars of each tab using windows functions.
    ; Searches for active tab using DOpus window title.
    ; Returns number of added paths.
    WinGetTitle, _title, % "ahk_id " winId

    ; Each tab has its own address bar, so we can use it to determine the path of each tab
    static ADDRESS_BAR_CLASS := "dopus.filedisplaycontainer"
    ; Defined in AutoHotkey source
    static WINDOW_TEXT_SIZE := 32767
    VarSetCapacity(_text, WINDOW_TEXT_SIZE * 2)

    ; Find the first address bar HWND
    ; https://learn.microsoft.com/en-us/windows/win32/api/winuser/nf-winuser-findwindowexw
    _previousId := DllCall("FindWindowExW", "ptr", winId, "ptr", 0, "str", ADDRESS_BAR_CLASS, "ptr", 0)
    _startId    := _previousId
    _paths      := []
    _active     := 1

    loop, 100 {
        ; Pass every HWND to GetWindowText() and get the content
        ; https://learn.microsoft.com/en-us/windows/win32/api/winuser/nf-winuser-getwindowtextw
        if DllCall("GetWindowTextW", "ptr", _previousId, "str", _text, "int", WINDOW_TEXT_SIZE) {
            if InStr(_text, _title) {
                if _activeTabOnly {
                    paths.push([_text, "Dopus.ico", 1, ""])
                    return 1
                }
                _active := A_Index
            }
            _paths.push([_text, "Dopus.ico", 1, ""])
        }
        _nextId := DllCall("FindWindowExW", "ptr", winId, "ptr", _previousId, "str", ADDRESS_BAR_CLASS, "ptr", 0)

        ; The loop iterates through all the tabs over and over again,
        ; so we must stop when it repeats
        if (_nextId = _startId)
            break

        _previousId := _nextId
    }

    ; Push the active tab to the global array first
    ; Remove duplicate and add the remaining tabs
    _count := _paths.length()
    if _paths.hasKey(_active)
        paths.push(_paths.removeAt(_active))

    paths.push(_paths*)
    return _count
}
